# Γραμμικός & Δικτυακός Προγραμματισμός: 1η Εργασία, 2019-2020

#### Τσιρπάνης Θεόδωρος (`dai19090`)

## Το πρόβλημα

Η εργασία ζητάει την υλοποίηση ενός συντακτικού αναλυτή (parser) που θα διαβάζει αρχεία που περιγράφουν γραμμικά προβλήματα στα πρότυπα του LINDO.

Ο συντακτικός αναλυτής θα πρέπει να αναγνωρίζει συμβολοσειρές που θα ανήκουν στην γλώσσα που περιγράφεται από την παρακάτω γραμματική.

Για τον συμβολισμό χρησιμοποιούνται η [EBNF] για τα μη-τερματικά σύμβολα, και κανονικές εκφράσεις για τα τερματικά.

```
// Τερματικά
Number = \d+(?:\.\d+)?(?:e[+-]?\d+)?

ST = st|s\.t\.|subject\s+to

X = x\d+

// Μη-τερματικά

First Variable =
                   X
    |      Number, X
    | "+", Number, X
    | "-", Number, X
    | "+",         X
    | "-",         X

More Variables =
      "+", Number, X
    | "-", Number, X
    | "+",         X
    | "-",         X

Expression = First Variable, {More Variables}

Objective =
      "min", Expression
    | "max", Expression

Signed Number =
           Number
    | "+", Number
    | "-", Number

Constraint =
      Expression, "=" , Signed Number
    | Expression, "<=", Signed Number
    | Expression, ">=", Signed Number

Constraints = Constraint, {Constraint}

End Maybe = ["end"]

Linear Programming Problem = Objective, ST, Constraints, End Maybe
```

Το αρχικό σύμβολο της γραμματικής είναι προφανώς το `Linear Programming Problem`. Η γλώσσα δεν κάνει διάκριση μεταξύ κεφαλαίων (case-insensitive). Όπως και στο LINDO, τα σχόλια αρχίζουν με το θαυμαστικό και τελειώνουν όταν τελειώσει η γραμμή (ή το αρχείο).

Ο parser, θα πρέπει να μετατρέψει το συντακτικό δέντρο του προβλήματος που του δόθηκε σε μια μορφή φιλική προς κάποιον λύτη, η οποία αποτελείται από πίνακες και περιγράφεται στην εκφώνηση.

## Το πρόγραμμα

Το πρόγραμμα υλοποιήθηκε με [την συναρτησιακή γλώσσα προγραμματισμού F#][fsharp], χρησιμοποιώντας [την βιβλιοθήκη συντακτικής ανάλυσης Farkle][farkle]. Η συντακτική ανάλυση πραγματοποιείται με τον αλγόριθμο [LALR], και η λεκτική ανάλυση με ένα [ντετερμινιστικό πεπερασμένο αυτόματο][dfa].

Όταν εκτελείται, το πρόγραμμα δημιουργεί από μια περιγραφή της παραπάνω γραμματικής τον πίνακα καταστάσεων LALR και το αυτόματο, τα οποία χρησιμοποιεί αμέσως μετά για την συντακτική ανάλυση του αρχείου εισόδου, το οποίο δέχεται από τη γραμμή εντολών. Τα αποτελέσματα εμφανίζονται στην κονσόλα. Σε περίπτωση συντακτικού λάθους, θα εμφανιστεί ανάλογο μήνυμα στην κονσόλα.

Ο πηγαίος κώδικας του προγράμματος αποτελείται από τρία αρχεία κώδικα (εκτός του αρχείου project) που συνολικά αριθμούν περίπου διακόσιες γραμμές κώδικα (εκτός των σχολίων). Τα δύο πρώτα είναι εκτενώς σχολιασμένα στα Ελληνικά. Ακολουθεί μια περιγραφή του ρόλου του καθενός:

* `LPTypes.fs`: Το αρχείο αυτό περιέχει τους τύπους δεδομένων που περιγράφουν τα γραμμικά προβλήματα.

* `LPLanguage.fs`: Το αρχείο αυτό περιγράφει με τη βοήθεια του Farkle, την γλώσσα των γραμμικών προβλημάτων, καθώς και το πώς να μετατραπούν τα τερματικά και οι παραγωγές της στους τύπους στο πρώτο αρχείο.

* `Program.fs`: Το μικρότερο από τα τρία αρχεία, περιέχει το σημείο εισόδου, διαβάζει την είσοδο από τη γραμμή εντολών, καλεί τον συντακτικό αναλυτή του Farkle, και εμφανίζει το αποτέλεσμα της συντακτικής ανάλυσης.

### Οδηγίες εκτέλεσης

Για την εκτέλεση του προγράμματος, απαιτείται η εγκατάσταση [της έκδοσης 3.1 του .NET Core SDK][dotnet]. Το πρόγραμμα (όπως και το SDK) δουλεύει και στα τρία μεγάλα λειτουργικά συστήματα υπολογιστών.

Μετά την εγκατάσταση του SDK, το πρόγραμμα μπορεί να εκτελεστεί ανοίγοντας μια γραμμή εντολών και εκτελώντας την εντολή `dotnet run <filename>`. Διαγνωστικές πληροφορίες για την συντακτική ανάλυση του δοθέντος αρχείου μπορούν να εμφανιστούν με την εντολή `dotnet run --debug <filename>`.

Μαζί με τον πηγαίο κώδικα δίνονται δύο δοκιμαστικά αρχεία γραμμικών προβλημάτων, στα οποία αναγράφεται στα σχόλια η αναμενόμενη έξοδος του προγράμματος για το κάθε ένα.

[ebnf]: https://en.wikipedia.org/wiki/Extended_Backus%E2%80%93Naur_form
[fsharp]: https://fsharp.org
[farkle]: https://teo-tsirpanis.github.io/Farkle
[lalr]: https://en.wikipedia.org/wiki/LALR_parser
[dfa]: https://en.wikipedia.org/wiki/Deterministic_finite_automaton
[dotnet]: https://dotnet.microsoft.com/download
